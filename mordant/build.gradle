apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

version = VERSION_NAME
group = GROUP

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.github.ajalt:colormath:1.1.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    pkgName = 'mordant'
    pkgDesc = 'Full-featured text styling for Kotlin command-line applications '
    githubRepo = 'github.com/ajalt/mordant'
    pkgLicense = 'Apache-2.0'
    pkgLicenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    devInfo = {
        id 'ajalt'
        name 'AJ Alt'
        url 'https://github.com/ajalt'
    }
    bintrayUser = System.getenv('BINTRAY_USER')
    bintrayKey = System.getenv('BINTRAY_API_KEY')
    mavenUserToken = System.getenv('MAVEN_USER_TOKEN')
    mavenUserPass = System.getenv('MAVEN_USER_PASS')
    deployDryRun = false
}

apply from: new File(rootDir, 'deploy.gradle')


